pragma solidity ^0.4.20;

import "../lib/SafeMath.sol";
import "./PLATPriceOracle.sol";
import "../BitGuildToken.sol";

contract BitGuildTopUp {
  using SafeMath for uint256;

  // Token contract
  BitGuildToken public token;

  // Oracle contract
  PLATPriceOracle public oracle;

  // Address where funds are collected
  address public wallet;

  event TokenPurchase(address indexed purchaser, uint256 value, uint256 amount);

  constructor(address _token, address _oracle, address _wallet) public {
    require(_token != address(0));
    require(_oracle != address(0));
    require(_wallet != address(0));

    token = BitGuildToken(_token);
    oracle = PLATPriceOracle(_oracle);
    wallet = _wallet;
  }

  // low level token purchase function
  function buyTokens() public payable {
    // calculate token amount to be created
    uint256 tokens = getTokenAmount(msg.value, oracle.ETHPrice());

    // Send tokens
    token.transfer(msg.sender, tokens);
    emit TokenPurchase(msg.sender, msg.value, tokens);

    // Send funds
    wallet.transfer(msg.value);
  }

  // Returns you how much tokens do you get for the wei passed
  function getTokenAmount(uint256 weiAmount, uint256 price) internal pure returns (uint256) {
    uint256 tokens = weiAmount.mul(price).div(1 ether);
    return tokens;
  }

  // Fallback function
  function () external payable {
    buyTokens();
  }

  // Retrieve locked tokens (for when this contract is not needed anymore)
  function retrieveTokens() public {
    require(msg.sender == wallet);
    uint256 tokensLeft = token.balanceOf(this);
    token.transfer(wallet, tokensLeft);
  }
}
